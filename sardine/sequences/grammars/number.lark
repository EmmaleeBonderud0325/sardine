// Basic number parsing syntax (calculator for lists)
// List of found currently non-parsable expressions:
// - [12,2]![24,2]
?start: sum
?sum: product
     | sum "+" product                                      -> addition
     | sum "-" product                                      -> substraction
?product: super_atom 
     | product "*" super_atom                               -> multiplication
     | product "/" super_atom                               -> division
     | product "|" super_atom                               -> choice
     | product ":" super_atom                               -> random_in_range
     | product "!" super_atom                               -> extend
     | product "!!" super_atom                              -> extend_repeat
?list: "[" sum ("," sum)* ","? "]"                          -> make_list
     | super_atom "_" super_atom                            -> generate_ramp
     | super_atom "_" super_atom ":" super_atom             -> generate_ramp_with_range 
?value: NUMBER                                              -> number
     | list
?super_atom: value
     | (atom|list) "**" (atom|list)                         -> power
?atom: value                                      
     | "-" atom                                             -> negation
     | "+" atom                                             -> id
     | "(" sum ")"                                
     | "r"                                                  -> get_random_number

%import common.NUMBER
%import common.WS_INLINE
%ignore WS_INLINE